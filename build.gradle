buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

configurations {
    deployJars
}

apply plugin: 'maven'
apply plugin: 'cauldron'

minecraft {
    version = '1.7.2'
    mcpVersion = '9.03'
    mainClass = 'cpw.mods.fml.relauncher.ServerLaunchWrapper'
    tweakClass = 'cpw.mods.fml.common.launcher.FMLTweaker'
    installerVersion = "1.4"
    subprojects {
        repositories {
            mavenLocal()
            maven {
                name = "sonatype"
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
            }
            flatDir {
                name "fileRepo"
                dirs "repo"
            }
        }
    }
}

group = 'net.minecraftforge'
ext.mcVersion = "1.7.2"
ext.cauldronVersion = "1"
ext.forgeVersion = "1098"
ext.bukkitVersion = "04"
version = "${mcVersion}-${cauldronVersion}.${forgeVersion}.${bukkitVersion}.${getBuild()}"

jenkins {
    job = 'Cauldron'
}

launch4j {
    jreMinVersion = '1.6.0'
}

def getBuild() {
    // check for jenkins build and if not found, return default
    ext.env = System.getenv()
    if(env.BUILD_NUMBER) {
        return "$env.BUILD_NUMBER"
    } else {
        return "0"
    }
}

tasks.packageUniversal {
    classifier = 'universal'
}

tasks.packageUniversal.manifest { 
    attributes([
    'Implementation-Vendor': 'Cauldron',
    'Implementation-Title': 'Cauldron',
    'Implementation-Version': 'git-Cauldron-'+project.version+getBuild(),
    'Forge-Version': '10.12.1.1098',
    'Specification-Vendor': 'Bukkit Team',
    'Specification-Title': 'Bukkit',
    'Specification-Version': '1.7.2-R0.4-SNAPSHOT'
    ])
}

// TODO CHANGE THIS POM AND STUFF

uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployJars

        if (project.hasProperty("filesmaven")) {
            logger.info('Publishing to files server')
            repository(url: project.filesmaven.url) {
                authentication(userName: project.filesmaven.username, privateKey: project.filesmaven.key)
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Minecraft Forge API'
            url 'https://github.com/MinecraftForge/MinecraftForge'

            scm {
                url 'https://github.com/MinecraftForge/MinecraftForge'
                connection 'scm:git:git://github.com/MinecraftForge/MinecraftForge.git'
                developerConnection 'scm:git:git@github.com:MinecraftForge/MinecraftForge.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/MinecraftForge/MinecraftForge/issues'
            }

            licenses {
                license {
                    name 'Forge Public License'
                    url 'https://raw.github.com/MinecraftForge/MinecraftForge/master/MinecraftForge-License.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'cpw'
                    name 'cpw'
                    roles { role 'developer' }
                }
                developer {
                    id 'LexManos'
                    name 'Lex Manos'
                    roles { role 'developer' }
                }
                developer {
                    id 'AbrarSyed'
                    name 'Abrar Syed'
                    roles { role 'contributor' }
                }
            }
        }
    }
}
